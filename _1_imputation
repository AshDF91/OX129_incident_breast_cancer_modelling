******************************************************************************** 
* Title: Multiple imputation for endpoint 1 models - risk of incident breast cancer  
*        diagnosis 
* Project: OX129  
* Input: "\\data\final_datasets\OX129_endpoint1.dta" 
* Output: multiply imputed dataset - "OX129_endpoint1_imputed.dta"
* Author: Ash Clift 
* Date: 27th January 2022  
* Notes: Multiply impute missing values for BMI, Townsend, alcohol intake, smoking, 
* and ethnicity. 5 imputations generated for use throughout model building and eval. 
******************************************************************************** 


************************************************ 
* Open the complete case dataset - not imputed * 
************************************************  
use "\data\final_datasets\Endpoint_1\OX129_endpoint1.dta", clear  


*********************************** 
* Format variables as appropriate * 
***********************************   

* Form 'previous gynaecological cancer' variable - composite of binary variables that cover: *
* ovarian cancer (ovarianca), uterine cancer (uterineca) and endometrial cancer (endometrialca) * 
gen previous_gynae_cancer = 0  
replace previous_gynae_cancer = 1 if ovarianca==1 | uterineca==1 | endometrialca==1 
tab previous_gynae_cancer  
drop ovarianca uterineca endometrialca  

* Ethnicity classification *  
tab ethnicgroupid      // 10 level category  

gen ethnicity = . 

* White British, White other * 
replace ethnicity = 1 if ethnicgroupid==1 | ethnicgroupid==2 | ethnicgroupid==3

* Indian * 
replace ethnicty = 2 if ethnicgroupid==8 

* Pakistani * 
replace ethnicity = 3 if ethnicgroupid==9 

* Bangladeshi * 
replace ethnicity = 4 if ethnicgroupid==10 

* Other Asian * 
replace ethnicity = 5 if ethnicgroupid==11 

* Black Caribbean * 
replace ethnicity = 6 if ethnicgroupid==12 

* Black African or other Black * 
replace ethnicity = 7 if ethnicgroupid==13 | ethnicgroupid==14 

* Chinese * 
replace ethnicity = 8 if ethnicgroupid==15 

* Other, inc. mixed race or Arab * 
replace ethnicity = 9 if ethnicgroupid==4 | ethnicgroupid==5 | ethnicgroupid==6 | ethnicgroupid==7 | ethnicgroupid==16 


********************************************************************* 
* Stset the data - estimate Nelson-Aalen cumulative hazard function * 
*********************************************************************  
* Tabulate events - status6 = incident breast cancer case *  
* exit3status = contains all cause death (use to define competing event), 1 = died, 0 = did not die *  

tab status6  
tab exit3status 
la de events 0 "Censored" 1 "Incident breast cancer" 2 "Died - competing risk" 
gen endpoint = 0  
replace endpoint = 1 if status6==2   // 1 = prevalent, 2 = incident case 
replace endpoint = 2 if (exit3status==1 & status6!=2) 
la val endpoint events   
tab endpoint  


* Make follow-up time variable *  
gen time = exit3date - enter3  
replace time = time/365.25        // scale is years  

* Stset the data *  
stset time, failure(endpoint==1) 

* Estimate Nelson-Aalen estimate of cumulative hazard function - use in imputation model *  
sts gen nelson_aalen = na  
summ nelson_aalen  


********************************************* 
* Set up data ready for multiple imputation * 
********************************************* 
* Examine missingness *  
tab smoke_cat, m  
tab alcohol_cat, m  
tab ethnicity, m  
summ baseline_bmi, det  
count if baseline_bmi==.  
count if town_int==.   

* Tab event counts in ethnic sub-groups before modelling *  
tab ethnicity endpoint, m  
tab ethnicity endpoint if period==2 , m  

* Assess for skew of BMI - ?needs log transform? *  
histogram baseline_bmi, percent normal   // yes, log transform better fits normal distribution visually 
gen ln_bmi = log(baseline_bmi) 
histogram ln_bmi, percent normal  

* Ranges of ln-transformed BMI for imputation model *  
summ ln_bmi, det    // we will impute the natural log, then transform back for analysis. 

* Fill in zeros for never-users of HRT *  
foreach x in recent_oestrogen past_oestrogen recent_combinedhrt past_combinedhrt {  
    replace `x' = 0 if `x'==. 
} 
tab recent_oestrogen  
tab past_oestrogen  
tab recent_combinedhrt  
tab past_combinedhrt 

* Set up data ready - mi set wide *  
mi set wide  

* Register variables to be imputed *  
mi register imputed smoke_cat alcohol_cat ln_bmi town_int ethnicity  

* Set the imputation model off *  
* This contains temporal period (i.period), N-A CHF (nelson_aalen) as auxiliary variables *  
* Imputation model contains endpoint indicator as well *  

timer on 1  
mi impute chained (ologit, ascontinuous) smoke_cat alcohol_cat (truncreg, ll(2.70805) ul(3.850147)) ///
ln_bmi (truncreg, ll(-7) ul(11)) town_int (mlogit, ascontinuous) ethnicity = age i.ckd i.pcos i.htn i.ihd  //
i.lungca i.bloodca i.thyroidca i.cirrhosis i.lupus i.vasculitis i.endometriosis i.psychosis i.fibrocystic  //
i.fh_breastca i.hysterectomy i.thiazide i.betablocker i.raa i.acei i.cablocker i.tca i.maoi i.ssri i.ocp  // 
i.recent_oestrogen i.past_oestrogen i.recent_combinedhrt i.past_combinedhrt i.type1dm i.type2dm i.fh_gynaeca //
i.anti_psychotic i.previous_biopsy i.previous_gynae_cancer i.period i.endpoint nelson_aalen,  // 
add(5) rseed(1066) augment chaindots  

timer off 1  
timer list  

************************ 
* Save imputed dataset * 
************************  
save "\\final_datasets\Endpoint_2\OX129_endpoint1_imputed.dta", replace  
clear  

******************************************************************************** 
********************************************************************************
